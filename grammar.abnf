;; Haga Configuration Language ABNF Specification
;;
;; Haga requires strict cardinality for all declarations.
;; Core ABNF Rules (RFC 5234) are assumed (ALPHA, DIGIT, SP).

;; -- Foundations --
manifest      = *statement
;; Content is optional to support empty lines and comments.
statement     = *space [ scope / declaration ] *space [comment] newline

;; --- Scoping ---
scope         = "scope" 1*space identifier 1*space block
declaration   = cardinality 1*space noun [ 1*space argument-list ]
block         = "{" newline *statement "}"

;; --- Keywords ---
cardinality   = "must" / "may" / "any" / "some"
noun          = identifier
argument-list = value *( "," space value )
identifier    = (ALPHA / "_" / UTF8-byte) *(ALPHA / DIGIT / "_" / UTF8-byte)

;; --- Values ---
value         = string / boolean / decimal / integer
boolean       = "true" / "false"
; Strict Integer: No leading zeros. No hex. No octal.
; Rejects "-0".
zero          = "0"
integer       = zero / ( ["-"] nonzero-digit *DIGIT )
; Decimal: Allows "-0.x".
decimal       = ["-"] ( "0" / nonzero-digit *DIGIT ) "." 1*DIGIT
; Helpers
nonzero-digit = %x31-39  ; 1-9

;; Double-quotes only, single-quotes are content, not delimiter:
string        = quote-mark *string-char quote-mark

;; --- Lexical Tokens ---
comment       = "#" *printable
space         = SP
newline       = LF
quote-mark    = %x22  ; "
UTF8-byte     = %x80-FF
; No raw tabs, no raw newlines.
unescaped     = %x20-21 / %x23-5B / %x5D-7E / UTF8-byte
escaped       = "\" ( "n" / "r" / "t" / "\" / quote-mark )
printable     = %x20-7E / UTF8-byte ; Printable ASCII characters plus UTF-8
string-char   = unescaped / escaped
